if (isRelease) {
    apply plugin: 'com.android.library'
}else {
    apply plugin: 'com.android.application'
}

//赋值与引用config.gradle
def rootAndroidId=rootProject.ext.androidId
def appId=rootProject.ext.appId
def support=rootProject.ext.dependencies

android {

    compileSdkVersion rootAndroidId.compileSdkVersion
    buildToolsVersion rootAndroidId.buildToolsVersion

    defaultConfig {
        //如果是集成化模式，不能有applicationId
        if (!isRelease){
            applicationId appId.personal
        }

        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"


        //第一个参数：确定值的类型，第二个参数：指定key的名字，第三个参数：传值（必须是String）
        //src代码中有可能需要用到跨模块交互，如果是组件化模块显然不行
        //不能配置在android根节点，只能在defaultConfig或buildType节点下
        buildConfigField("boolean","isRelease",String.valueOf(isRelease))

        //在gradle文件中配置选项参数值（用于APT传参接收，在ARouterProcessor的参数）
        //该配置必须卸载defaultConfig节点下
        javaCompileOptions{
            annotationProcessorOptions{
                arguments=[moduleName:project.name,packageNameForAPT:packageNameForAPT]
            }

        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //源集-设置源集的属性，更改源集的Java目录或自由目录等(配置资源路径，方便测试环境，打包不集成到正式环境)
    sourceSets {
        main{
            if(!isRelease){
                //如果是组件化模式，需要单独运行时
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java.srcDirs=['src/main/java']
//                    res.srcDir=['src/main/res']
//                    resources.srcDirs=['src/main/resources']
//                    aidl.srcDirs=['src/main/aidl']
//                    assets.srcDirs=['src/main/assets']

            }else {
                //集成化模式，整个项目打包
                manifest.srcFile 'src/main/AndroidManifest.xml'

                java{

                    //release 时 debug 目录下文件不会合并到主工程
                    exclude '**/debug/**'
                }

            }

        }

    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation project(':common')
    //引入annotation，让注解处理器处理注解
    implementation project(":annotation")
    //注解处理器
    annotationProcessor project(':compiler')

}
